plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'androidx.navigation.safeargs.kotlin'
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def keystorePropertiesFile = rootProject.file("keystore.properties")
project.ext.keystoreProperties = new Properties()
if (keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def getKeystoreProperty(String key) {
    return System.getenv(key) ?: project.keystoreProperties[key]
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.ariefzuhri.gizee"
        minSdk 21
        targetSdk 31
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(getKeystoreProperty("STORE_FILE"))
            storePassword getKeystoreProperty("STORE_PASSWORD")
            keyAlias getKeystoreProperty("KEY_ALIAS")
            keyPassword getKeystoreProperty("KEY_PASSWORD")
        }
    }

    buildTypes {
        // End-user, stable version
        release {
            signingConfig signingConfigs.release

            debuggable false

            minifyEnabled true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'
            ), 'proguard-rules.pro'

            manifestPlaceholders = [
                    isAnalyticsEnabled  : true,
                    isCrashlyticsEnabled: true,
            ]
        }

        // End-user, beta version
        beta {
            signingConfig signingConfigs.release

            debuggable false

            minifyEnabled true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'
            ), 'proguard-rules.pro'

            manifestPlaceholders = [
                    isAnalyticsEnabled  : true,
                    isCrashlyticsEnabled: true,
            ]

            versionNameSuffix "-beta${versionBuild}"
        }

        // Internal test, non-technical team
        alpha {
            signingConfig signingConfigs.debug

            debuggable true

            minifyEnabled true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'
            ), 'proguard-rules.pro'

            manifestPlaceholders = [
                    isAnalyticsEnabled  : false,
                    isCrashlyticsEnabled: true,
            ]

            versionNameSuffix "-alpha${versionBuild}"
        }

        // Internal test, technical team
        debug {
            signingConfig signingConfigs.debug

            debuggable true

            manifestPlaceholders = [
                    isAnalyticsEnabled  : false,
                    isCrashlyticsEnabled: true,
            ]

            versionNameSuffix "-dev${versionBuild}"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    dynamicFeatures = [
            ':feature:details',
            ':feature:favorites',
            ':feature:fullnutrients',
            ':feature:history',
            ':feature:home',
            ':feature:nutritionfacts',
            ':feature:searchresults',
    ]

    lint {
        abortOnError false
    }
}

dependencies {
    api project(":core:common")
    api project(":core:database")

    api project(':library:bottomsheet')

    // Splash screen API
    implementation "androidx.core:core-splashscreen:$splashScreenVersion"

    // Navigation component
    api "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    api "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    api "androidx.navigation:navigation-dynamic-features-fragment:$navigationVersion"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:$firebaseVersion")
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
}