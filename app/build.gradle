plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}
apply from: '../shared-dependencies.gradle'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def keystorePropertiesFile = rootProject.file("keystore.properties")
project.ext.keystoreProperties = new Properties()
if (keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def getKeystoreProperty(String key) {
    return System.getenv(key) ?: project.keystoreProperties[key]
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.ariefzuhri.gizee"
        minSdk 21
        targetSdk 31
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(getKeystoreProperty("STORE_FILE"))
            storePassword getKeystoreProperty("STORE_PASSWORD")
            keyAlias getKeystoreProperty("KEY_ALIAS")
            keyPassword getKeystoreProperty("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    isAnalyticsEnabled  : true,
                    isCrashlyticsEnabled: true,
            ]
        }

        beta {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    isAnalyticsEnabled  : true,
                    isCrashlyticsEnabled: true,
            ]
            versionNameSuffix "-beta.${versionBuild}"
        }

        debug {
            manifestPlaceholders = [
                    isAnalyticsEnabled  : false,
                    isCrashlyticsEnabled: false,
            ]
            versionNameSuffix "-alpha.${versionBuild}"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        abortOnError false
    }

    dynamicFeatures = [':favorites']
}

dependencies {
    implementation project(":core")
    implementation project(':bottomsheet')
    implementation project(":nutritionfactslabel")

    // Chart
    implementation "com.github.AAChartModel:AAChartCore-Kotlin:$aaChartCoreVersion"
}