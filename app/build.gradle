plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}
apply from: '../shared-dependencies.gradle'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def keystorePropertiesFile = rootProject.file("keystore.properties")
project.ext.keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def getKeystoreField(String key) {
    return System.getenv(key) ?: project.keystoreProperties[key]
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.ariefzuhri.gizee"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(getKeystoreField("storeFile"))
            storePassword getKeystoreField("storePassword")
            keyAlias getKeystoreField("keyAlias")
            keyPassword getKeystoreField("keyPassword")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsEnabled: true]
        }

        beta {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsEnabled: true]
            versionNameSuffix "-beta.${versionBuild}"
        }

        debug {
            manifestPlaceholders = [crashlyticsEnabled: false]
            versionNameSuffix "-alpha.${versionBuild}"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    lintOptions {
        abortOnError false
    }

    dynamicFeatures = [':favorites']
}

dependencies {
    implementation project(":core")
    implementation project(":bottomsheetdialogfragment")
    implementation project(":nutritionfactslabel")

    // Chart
    implementation "com.github.AAChartModel:AAChartCore-Kotlin:$aaChartCoreVersion"
}